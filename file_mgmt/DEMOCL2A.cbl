000100 IDENTIFICATION DIVISION.
000200  PROGRAM-ID. DEMOCL2A.
000300  AUTHOR. TOPICTRICK.
000400  DATE-WRITTEN.  03-MAR-2023.
000500  DATE-COMPILED. 03-MAR-2023.
000600*
000700* -----------------------------------------------------------*
000800*    PROJ: Demo Class 11 A. 
000900*          Req: Generate monthly report for list of employee whose
001000*               process code is 'P' in the list file. The program  
001100*               will fetch employee details from the 
001200*               employee master file. 
001300*  
001400*     Purpose: This program demonstrate the COBOL File Handling
001500*              Capabilities. This program will demonstrate the 
001600*              following file operations.  
001700*              * Read Data from the sequential file. 
001800*              * Write Data to a sequential file. 
001900*
002000*        Files: All files are of fixed length.  
002100*              Input  - Employee List File    (Sequential). 
002200*                     - Employee Detail File  (Sequential). 
002300*              Output - Monthly Report        (Sequential).
002400*
002500*        Assumption: Input files are sorted.
002600*                    Input files does not have any duplicates.  
002700* -----------------------------------------------------------*            
002800 ENVIRONMENT DIVISION.                                            
002900  INPUT-OUTPUT SECTION.                                           
003000  FILE-CONTROL.  
003100     SELECT EMP-LIST ASSIGN 
                           TO  '/Users/simonvargas/EMPFLE.LIST.txt'                           
003200            ORGANIZATION IS SEQUENTIAL.  
003300*
003400     SELECT EMP-FILE ASSIGN 
                           TO  '/Users/simonvargas/EMP.MNTH.FILE.txt'                          
003500            ORGANIZATION IS SEQUENTIAL.                           
003600*                                                                 
003700     SELECT REP-FILE ASSIGN TO '/Users/simonvargas/EMPRPT.txt'                             
003800            ORGANIZATION IS SEQUENTIAL.                           
003900*                                                                 
004000 DATA DIVISION.
004100  FILE SECTION.
004200  FD EMP-LIST.
004300  01 EMP-LIST-REC.
004400     05 EMPL-PCDE            PIC X(01).
004500     05 FILLER               PIC X(01).
004600     05 EMPL-EMPNO           PIC 9(06).
004700     05 FILLER               PIC X(72).
004800*
004900  FD EMP-FILE.
005000  COPY EMPRECC REPLACING ==(EP)==  BY ==IN==.
005100*
005200  FD REP-FILE.
005300  01 REP-FILE-REC               PIC X(150).
005400*
005500 WORKING-STORAGE SECTION.
005600*
005700  01 WS-SWITCH.
005800     05 END-OF-FILE-SWITCH      PIC X(01)  VALUE 'N'.
005900        88 END-OF-FILE                     VALUE 'Y'.
006000        88 NOT-END-OF-FILE                 VALUE 'N'.
006100*
006200  01 WS-COUNTERS.
006300     05 WS-INP-REC              PIC 9(05).
006400     05 WS-OUT-REC              PIC 9(05).
006500*
006600  01 WS-TEMP-DATE.
006700     05 WS-HDTE                 PIC 9(08).
006800     05 THIS REDEFINES WS-HDTE.
006900        10 WS-HDTE-DD           PIC X(02).
007000        10 WS-HDTE-MM           PIC X(02).
007100        10 WS-HDTE-YYYY         PIC X(04).
007200     05 WS-TDY-DTE              PIC 9(08).
007300     05 THIS REDEFINES WS-TDY-DTE.
007400        10 WS-TDYDTE-YYYY       PIC 9(04).
007500        10 WS-TDYDTE-MM         PIC 9(02).
007600        10 WS-TDYDTE-DD         PIC 9(02).
007700
007800*
007900  01 HEAD1.
008000     05 FILLER            PIC X(60) VALUE SPACES.
008100     05 FILLER            PIC X(10) VALUE ' EMPLOYEE '.
008200     05 FILLER            PIC X(25) VALUE 'MANAGEMENT SYSTEM.'.
008300     05 FILLER            PIC X(41) VALUE SPACES.
008400     05 HD-DTE.
008500        10 HD-DTE-DD      PIC X(02) VALUE SPACES.
008600        10 FILLER         PIC X(01) VALUE '/'.
008700        10 HD-DTE-MM      PIC X(02) VALUE SPACES.
008800        10 FILLER         PIC X(01) VALUE '/'.
008900        10 HD-DTE-YYYY    PIC X(04) VALUE SPACES.
009000        10 HD-DOT         PIC X(01) VALUE '.'.
009100        10 HD-FILLER      PIC X(03) VALUE SPACES.
009200     66 WS-PGM-SUMY-DTE RENAMES HD-DTE-DD THRU HD-FILLER.
009300*
009400  01 HEAD2.
009500     05 FILLER            PIC X(70) VALUE SPACES.
009600     05 FILLER            PIC X(11) VALUE 'DUBLIN, IRL'.
009700     05 FILLER            PIC X(69) VALUE SPACES.
009800*
009900  01 COLHEAD3.
010000     05 FILLER            PIC X(04) VALUE SPACES.
010100     05 FILLER            PIC X(05) VALUE 'EMPNO'.
010200     05 FILLER            PIC X(02) VALUE SPACES.
010300     05 FILLER            PIC X(06) VALUE 'F NAME'.
010400     05 FILLER            PIC X(08) VALUE SPACES.
010500     05 FILLER            PIC X(07) VALUE 'M INIIT'.
010600     05 FILLER            PIC X(01) VALUE SPACES.
010700     05 FILLER            PIC X(06) VALUE 'L NAME'.
010800     05 FILLER            PIC X(11) VALUE SPACES.
010900     05 FILLER            PIC X(06) VALUE 'W DEPT'.
011000     05 FILLER            PIC X(03) VALUE SPACES.
011100     05 FILLER            PIC X(05) VALUE 'PH NO'.
011200     05 FILLER            PIC X(03) VALUE SPACES.
011300     05 FILLER            PIC X(06) VALUE 'H DATE'.
011400     05 FILLER            PIC X(05) VALUE SPACES.
011500     05 FILLER            PIC X(06) VALUE 'JOB TY'.
011600     05 FILLER            PIC X(04) VALUE SPACES.
011700     05 FILLER            PIC X(08) VALUE 'ED LEVEL'.
011800     05 FILLER            PIC X(02) VALUE SPACES.
011900     05 FILLER            PIC X(03) VALUE 'SEX'.
012000     05 FILLER            PIC X(02) VALUE SPACES.
012100     05 FILLER            PIC X(10) VALUE 'BIRTH DATE'.
012200     05 FILLER            PIC X(05) VALUE SPACES.
012300     05 FILLER            PIC X(06) VALUE 'SALARY'.
012400     05 FILLER            PIC X(05) VALUE SPACES.
012500     05 FILLER            PIC X(05) VALUE 'BONUS'.
012600     05 FILLER            PIC X(05) VALUE SPACES.
012700     05 FILLER            PIC X(04) VALUE 'COMM'.
012800     05 FILLER            PIC X(07) VALUE SPACES.
012900
013000* Report file detail line header.
013100
013200  01 DTL-LINE.
013300     05 FILLER               PIC X(03) VALUE SPACES.
013400     05 DTL-EMPNO            PIC Z(06).
013500     05 FILLER               PIC X(02) VALUE SPACES.
013600     05 DTL-FIRSTNME         PIC X(12).
013700     05 FILLER               PIC X(04) VALUE SPACES.
013800     05 DTL-MIDINIT          PIC X(01).
013900     05 FILLER               PIC X(05) VALUE SPACES.
014000     05 DTL-LASTNAME         PIC X(17).
014100     05 FILLER               PIC X(02) VALUE SPACES.
014200     05 DTL-WORKDEPT         PIC Z(03).
014300     05 FILLER               PIC X(05) VALUE SPACES.
014400     05 DTL-PHONENO          PIC Z(04).
014500     05 FILLER               PIC X(02) VALUE SPACES.
014600     05 DTL-HIREDATE.
014700        10 DTL-HIREDATE-DD   PIC 9(02).
014800        10 FILLER            PIC X(01) VALUE '/'.
014900        10 DTL-HIREDATE-MM   PIC 9(02).
015000        10 FILLER            PIC X(01) VALUE '/'.
015100        10 DTL-HIREDATE-YYYY PIC 9(04).
015200     05 FILLER               PIC X(03) VALUE SPACES.
015300     05 DTL-JOB              PIC X(08).
015400     05 FILLER               PIC X(04) VALUE SPACES.
015500     05 DTL-EDLEVEL          PIC Z(02).
015600     05 FILLER               PIC X(06) VALUE SPACES.
015700     05 DTL-SEX              PIC X(01).
015800     05 FILLER               PIC X(03) VALUE SPACES.
015900     05 DTL-BIRTHDATE.
016000        10 DTL-BRTHDATE-DD   PIC 9(02).
016100        10 FILLER            PIC X(01) VALUE '/'.
016200        10 DTL-BRTHDATE-MM   PIC 9(02).
016300        10 FILLER            PIC X(01) VALUE '/'.
016400        10 DTL-BRTHDATE-YYYY PIC 9(04).
016500     05 FILLER               PIC X(03) VALUE SPACES.
016600     05 DTL-SALARY           PIC ZZZZZZ9.99.
016700     05 FILLER               PIC X(03) VALUE SPACES.
016800     05 DTL-BONUS            PIC ZZZ9.99.
016900     05 FILLER               PIC X(03) VALUE SPACES.
017000     05 DTL-COMM             PIC ZZZ9.
017100     05 FILLER               PIC X(07) VALUE SPACES.
017200*
017300 01 TRL-LINE.
017400     05 FILLER               PIC X(04)  VALUE SPACES.
017500     05 TRL-LINE-MSG         PIC X(27)  VALUE SPACES.
017600     05 TRL-COUNT            PIC 9(05)  VALUE ZEROES.
017700     05 FILLER               PIC X(114) VALUE SPACES.
017800*
017900 01 RPT-BLK-LNE.
018000    05 RPT-BLK-AST           PIC X(01)  VALUE '*'.
018100    05 RPT-BLK-SPC           PIC X(149) VALUE SPACES.
018200*
018300 PROCEDURE DIVISION.
018400 0000-CORE-BUSINESS-LOGIC.
018500     PERFORM A000-INIT-VALS
018600     PERFORM B000-OPEN-FILE
018700     PERFORM C000-PRNT-HDRS
018800     PERFORM E000-READ-LIST-FILE
018900     PERFORM F000-READ-EMPLY-FILE
019000     PERFORM D000-PROCESS-RECDS
019100     PERFORM X000-CLSE-FILE
019200     STOP RUN.
019300*
019400 A000-INIT-VALS SECTION.
019500 A010-INIT-TMP-VALS.
019600     INITIALIZE WS-COUNTERS, DTL-LINE, TRL-LINE,
019700                WS-TEMP-DATE.
019800*
019900 A099-EXIT.
020000      EXIT.
020100*
020200 B000-OPEN-FILE SECTION.
020300 B010-OPEN-FILE.
020400      OPEN INPUT  EMP-LIST, EMP-FILE
020500           OUTPUT REP-FILE.
020600 B099-EXIT.
020700      EXIT.
020800*
020900 C000-PRNT-HDRS SECTION.
021000 C010-MOVE-TDY-DATE.
021100      ACCEPT WS-TDY-DTE    FROM DATE YYYYMMDD
021200      MOVE WS-TDYDTE-DD    TO HD-DTE-DD
021300      MOVE WS-TDYDTE-MM    TO HD-DTE-MM
021400      MOVE WS-TDYDTE-YYYY  TO HD-DTE-YYYY.
021500
021600 C020-PRNT-RPT-HDRS.
021700      WRITE REP-FILE-REC FROM HEAD1.
021800      WRITE REP-FILE-REC FROM HEAD2.
021900      WRITE REP-FILE-REC FROM RPT-BLK-LNE.
022000      WRITE REP-FILE-REC FROM COLHEAD3.
022100      WRITE REP-FILE-REC FROM RPT-BLK-LNE.
022200*
022300 C099-EXIT.
022400      EXIT.
022500*
21000  D000-PROCESS-RECDS SECTION.
022700 D010-PROCESS-RECDS.
022800      PERFORM UNTIL END-OF-FILE
022900           EVALUATE TRUE
023000               WHEN EMPL-EMPNO > IN-EMPNO
023100                    PERFORM F000-READ-EMPLY-FILE
023200               WHEN EMPL-EMPNO < IN-EMPNO
023300                    PERFORM E000-READ-LIST-FILE
023400               WHEN EMPL-EMPNO = IN-EMPNO
023500                    IF EMPL-PCDE = 'P'
023600                       PERFORM G000-PRNT-REPT
023700                    END-IF
023800                    PERFORM E000-READ-LIST-FILE
023900                    PERFORM F000-READ-EMPLY-FILE
024000               WHEN OTHER
024100                    CONTINUE
024200           END-EVALUATE
024300      END-PERFORM.
024400*
024500 D099-EXIT.
024600      EXIT.
024700
024800 E000-READ-LIST-FILE SECTION.
024900 E010-READ-LIST-FILE.
025000        READ EMP-LIST
025100             AT END      SET END-OF-FILE TO TRUE
025200             NOT AT END  ADD +1          TO WS-INP-REC 
025300        END-READ.
025400*
025500 E099-EXIT.
025600      EXIT.
025700*
025800 F000-READ-EMPLY-FILE SECTION.
025900 F010-READ-EMPLY-FILE.
026000         READ EMP-FILE
026100              AT END   SET END-OF-FILE TO TRUE
026200                       DISPLAY 'RECORD NOT FOUND ', EMPL-EMPNO
026300         END-READ.
026400*
026500 F099-EXIT.
026600      EXIT.
026700*
026800 G000-PRNT-REPT SECTION.
026900 G010-MOVE-DTL-REC.
027000     MOVE  IN-EMPNO        TO DTL-EMPNO
027100     MOVE  IN-FIRSTNME     TO DTL-FIRSTNME
027200     MOVE  IN-MIDINIT      TO DTL-MIDINIT
027300     MOVE  IN-LASTNAME     TO DTL-LASTNAME
027400     MOVE  IN-WORKDEPT     TO DTL-WORKDEPT
027500     MOVE  IN-PHONENO      TO DTL-PHONENO
027600     MOVE  ZEROES          TO WS-HDTE
027700     MOVE  IN-HIREDATE     TO WS-HDTE
027800     MOVE  WS-HDTE-DD      TO DTL-HIREDATE-DD
027900     MOVE  WS-HDTE-MM      TO DTL-HIREDATE-MM
028000     MOVE  WS-HDTE-YYYY    TO DTL-HIREDATE-YYYY
028100     MOVE  IN-JOBTY        TO DTL-JOB
028200     MOVE  IN-EDLEVEL      TO DTL-EDLEVEL
028300     MOVE  IN-SEX          TO DTL-SEX
028400     MOVE  ZEROES          TO WS-HDTE
028500     MOVE  IN-BIRTHDATE    TO WS-HDTE
028600     MOVE  WS-HDTE-DD      TO DTL-BRTHDATE-DD
028700     MOVE  WS-HDTE-MM      TO DTL-BRTHDATE-MM
028800     MOVE  WS-HDTE-YYYY    TO DTL-BRTHDATE-YYYY
028900     MOVE  IN-SALARY       TO DTL-SALARY
029000     MOVE  IN-BONUS        TO DTL-BONUS
029100     MOVE  IN-COMM         TO DTL-COMM.
029200
029300*
029400 G020-WRITE-DTL-REC.
029500     WRITE REP-FILE-REC FROM DTL-LINE.
029600     INITIALIZE DTL-LINE
029700     ADD +1   TO WS-OUT-REC.
029800*
029900 G099-EXIT.
030000      EXIT.
030100*
030200 X000-CLSE-FILE SECTION.
030300 X010-PRNT-TRL-REC.
030400     WRITE REP-FILE-REC FROM RPT-BLK-LNE
030500*
030600     MOVE 'TOTAL NO OF RECORD READ  :' TO TRL-LINE-MSG
030700     MOVE  WS-INP-REC    TO TRL-COUNT
030800     WRITE REP-FILE-REC FROM TRL-LINE
030900*
031000     MOVE  SPACES        TO TRL-LINE-MSG
031100     MOVE  ZEROES        TO TRL-COUNT
031200*
031300     MOVE 'TOTAL NO OF RECORD PRINT :' TO TRL-LINE-MSG
031400     MOVE  WS-OUT-REC    TO TRL-COUNT
031500*
031600     WRITE REP-FILE-REC FROM TRL-LINE.
031700
031800 X020-CLOSE-FILE.
031900     CLOSE EMP-LIST, EMP-FILE, REP-FILE.
032000*
032100 X020-PRINT-TOTALS.
032200     DISPLAY '****** PROGRAM SUMMARY ****************'
032300     DISPLAY 'PGM EXECUTION DATE       :', HD-DTE
032400     DISPLAY 'TOTAL NO OF RECORD READ  :', WS-INP-REC
032500     DISPLAY 'TOTAL NO OF RECORD PRINT :', WS-OUT-REC
032600     DISPLAY '****************************************'.
032700*
032800 X099-EXIT.
032900      EXIT.
033000